ext {
    groupId = "com.github.thibseisel"
    artifactId = "kdenticon"
}

group groupId
artifactId = artifactId
version libVersion

buildscript {
    ext.junit_platform_version = '1.0.0'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:${junit_platform_version}"
    }
}

apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "junit:junit:$junit_version"

    testImplementation ("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.apikey')
    publish = true
    override = true

    pkg {
        repo = 'maven'
        name = artifactId
        configurations = ['archives']

        version {
            name = libVersion
            released = new Date()
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId groupId
            artifactId artifactId
            version libVersion
            name artifactId
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.kotlin.srcDirs
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    reportNotDocumented = false
}

task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
    classifier = 'javadoc'
    from fileTree(new File(project.buildDir, 'javadoc'))
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
