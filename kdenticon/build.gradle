/*
 * Copyright 2017 Thibault Seisel
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "junit:junit:$junit_version"

    testImplementation ("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

String libraryName = 'kdenticon'

group rootProject.libGroup
version rootProject.libVersion

install {
    repositories.mavenInstaller {
        pom.project {
            name libraryName
            description 'A library for generating highly recognizable identicons for the JVM platform.'
            url 'https://github.com/thibseisel/Kdenticon'
            inceptionYear '2017'

            packaging 'jar'
            groupId rootProject.libGroup
            artifactId libraryName
            version rootProject.libVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/thibseisel/Kdenticon.git'
                url 'https://github.com/thibseisel/Kdenticon'
            }

            developers {
                developer {
                    name 'Thibault Seisel'
                }
            }
        }
    }
}

bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.apikey')
    configurations = ['archives']

    pkg {
        repo = 'android'
        name = 'kdenticon'
        publish = false
        override = true

        version {
            name = rootProject.libVersion
            released = new Date()
        }
    }
}

// Creates a Jar containing source code to help debugging
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

// Creates javadoc from KDoc tags
task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    reportNotDocumented = false
}

// Creates a Jar containing javadoc. Happens after dokkaJavadoc task.
task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
    classifier = 'javadoc'
    from fileTree(new File(project.buildDir, 'javadoc'))
}

// Adds Javadoc and sources to Maven artifacts
artifacts {
    archives javadocJar
    archives sourcesJar
}
